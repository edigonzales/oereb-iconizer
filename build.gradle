plugins {
    id 'java-library'
    id 'application'    
    id "com.jfrog.bintray" version "1.8.4"     
}

apply plugin: 'maven-publish'
apply from: "gradle/versioning.gradle"

repositories {
    jcenter()
}

dependencies {
    api 'org.slf4j:slf4j-api:1.7.26'
    api 'org.slf4j:slf4j-simple:1.7.26'
    api 'org.apache.httpcomponents:httpclient:4.5.6'
    api 'commons-io:commons-io:2.6'
    api 'org.postgresql:postgresql:42.2.5'
    
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.4.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.4.1'
    testImplementation 'org.testcontainers:testcontainers:1.10.7'
    testImplementation 'org.testcontainers:postgresql:1.10.7'
    testImplementation 'org.testcontainers:junit-jupiter:1.10.7'
    //testImplementation 'org.postgresql:postgresql:42.2.5'    
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = 'ch.so.agi.oereb'

distZip {
    archiveName 'iconizer' + '-'  + project.version.toString() + '.zip'
}

distTar {
    archiveName 'iconizer' + '-'  + project.version.toString() + '.tar'
}

application {
    mainClassName = "ch.so.agi.oereb.Main"
}

run {
    //args = ["--help"]
    args = ["--downloadDir", "/Users/stefan/tmp/", "--sldUrl", "https://wms.geo.gl.ch/?content=None&request=GetStyles&srs=EPSG:2056&LAYERS=ch.gl.inlandwaters.oereb_grundwasserschutzzonen&service=WMS&version=1.3.0", "--legendGraphicUrl", "https://wms.geo.gl.ch/?SERVICE=WMS&VERSION=1.3.0&REQUEST=GetLegendGraphic&LAYER=ch.gl.inlandwaters.oereb_grundwasserschutzzonen&FORMAT=image/png&STYLE=default&SLD_VERSION=1.1.0&RULELABEL=false&LAYERTITLE=false&HEIGHT=35&WIDTH=70&SYMBOLHEIGHT=3&SYMBOLWIDTH=6&DPI=300"]
}

test {
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform {}
    failFast = true
}

// Bintray / jcenter
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

def githubRepoUrl = "https://github.com/openoereb/oereb-iconizer"
def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://github.com/openoereb/oereb-iconizer/blob/master/LICENSE"
        }
    }
    developers {
        developer {
            id "edigonzales"
            name "Stefan Ziegler"
            email "stefan.ziegler@bd.so.ch"
        }
    }

    scm {
        url githubRepoUrl
    }
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            artifact distZip
            
            groupId project.group
            artifactId 'oereb-iconizer'
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Creates icons for OEREB-Rahmenmodell and saves them in a database table as bytea.')
                root.appendNode('name', 'oereb-iconizer')
                root.appendNode('url', githubRepoUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.env.bintrayUser
    key = System.env.bintrayKey
    publications = ['mavenPublication']
    publish = true
    
    pkg {
        repo = 'oereb-iconizer'
        name = 'oereb-iconizer'
        userOrg = 'sogis'
        licenses = ['MIT']
        labels = ['oereb', 'plr', 'java']        
        vcsUrl = githubRepoUrl
        version {
            name = project.version
            desc = project.version
            released  = new Date()
        }
    }
}

